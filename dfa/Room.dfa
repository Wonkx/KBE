#! UG/KF 17.0
DefClass: Room (ug_base_part);
(Number Parameter) RoomHeight: 114;
(Number Parameter) RoomHeight_min: 94;
(Number Parameter) RoomHeight_max: 114;
(Number Parameter) RoomWidth: 156;
(Number Parameter) RoomWidth_min: 100;
(Number Parameter) RoomWidth_max: 156;
(Number Parameter) RoomLength: 152;
(Number Parameter) RoomLength_min: 100;
(Number Parameter) RoomLength_max: 204;
(Point Parameter) RoomOrigin: point(0,0,0);
(Child) rightwall: {
  class; Wall;
  WallHeight; RoomHeight:-child:WallThickness:;
  WallWidth; RoomWidth:;
  WallOrigin; RoomOrigin: +
  vector(RoomLength:-child:WallThickness:,
  0,
  child:WallThickness:);
  Door?; TRUE;
  Window?; TRUE;
  };
(Child) leftwall: {
  class; Wall;
  WallHeight; RoomHeight:-child:WallThickness:;
  WallWidth; RoomWidth:;
  referenceFrame; framexy(RoomOrigin: +
  vector(child:WallThickness:,
  RoomWidth:,
  child:WallThickness:),
  vector(-1,0,0),vector(0,-1,0));
  Door?; TRUE;
  };
(Child) frontwall: {
  class; Wall;
  WallHeight; RoomHeight:-child:WallThickness:;
  WallWidth; RoomLength:;
  referenceFrame; framexy(RoomOrigin: +
  vector(0,
  child:WallThickness:,
  child:WallThickness:),
  vector(0,-1,0),vector(1,0,0));
  Door?; TRUE;
  };
(Child) backwall: {
  class; Wall;
  WallHeight; RoomHeight:-child:WallThickness:;
  WallWidth; RoomLength:;
  referenceFrame; framexy(RoomOrigin: +
  vector(RoomLength:,
  RoomWidth:-child:WallThickness:,
  child:WallThickness:),
  vector(0,1,0),vector(-1,0,0));
  Door?; TRUE;
  };
(Child) floor: {
  class; ug_block;
  Length; RoomLength:;
  Width; RoomWidth:;
  Height; rightwall:WallThickness:;
  Origin; RoomOrigin:;
  };
(Child) ceiling: {
  class; ug_block;
  Length; RoomLength:;
  Width; RoomWidth:;
  Height; rightwall:WallThickness:;
  Origin; RoomOrigin:+vector(0,0,RoomHeight:-rightwall:WallThickness:);
  };
